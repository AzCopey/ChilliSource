//
//  android-build.gradle
//  Chilli Source
//  Created by Ian Copland on 13/03/2015.
//
//  The MIT License (MIT)
//
//  Copyright (c) 2015 Tag Games Limited
//
//  Permission is hereby granted, free of charge, to any person obtaining a copy
//  of this software and associated documentation files (the "Software"), to deal
//  in the Software without restriction, including without limitation the rights
//  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//  copies of the Software, and to permit persons to whom the Software is
//  furnished to do so, subject to the following conditions:
//
//  The above copyright notice and this permission notice shall be included in
//  all copies or substantial portions of the Software.
//
//  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
//  THE SOFTWARE.
//

apply from: '../../../../ChilliSource/Tools/Scripts/android-build-manifest.gradle'
apply from: '../../../../ChilliSource/Tools/Scripts/android-build-ndk.gradle'
apply from: '../../../../ChilliSource/Tools/Scripts/android-build-resources.gradle'

import com.sun.tools.classfile.Dependency

/**
 * @author Ian Copland
 *
 * @param in_taskName - The name of the task.
 *
 * @return Whether or not the given task name follows the convention set for Task tasks.
 */
def isTestFromTaskName(String in_taskName) {
    String lowerTaskName = in_taskName.toLowerCase()
    if (lowerTaskName.contains('unittest') == true || lowerTaskName.contains('androidtest') == true) {
        return true
    }
    return false
}
/**
 * @author Ian Copland
 *
 * @param in_taskName - The name of the task.
 *
 * @return Whether or not the given task name follows the convent set for debug tasks.
 */
def isDebugFromTaskName(String in_taskName) {
    String lowerTaskName = in_taskName.toLowerCase()
    if (lowerTaskName.contains('debug') == true) {
        return true
    } else if (lowerTaskName.contains('release') == true){
        return false
    } else {
        System.err.println("Task '$taskName' does not specify release or debug.")
        return false
    }
}
/**
 * @author Ian Copland
 *
 * @param in_taskName - The name of the task.
 *
 * @return The name of the ABI the task relates to, based on the naming convention.
 */
def getAbiFromTaskName(String in_taskName) {
    String lowerTaskName = in_taskName.toLowerCase()
    if (lowerTaskName.contains('armv7') == true) {
        return "armeabi-v7a"
    } else if (lowerTaskName.contains('arm')){
        return "armeabi"
    } else if (lowerTaskName.contains('x86')){
        return "x86"
    } else {
        System.err.println("Task '$taskName' does not specify an abi.")
        return "armeabi"
    }
}
/**
 * @author Ian Copland
 *
 * @param in_taskName - The name of the task.
 *
 * @return The name of the SKU the task relates to, based on the naming convention.
 */
def getSkuFromTaskName(String in_taskName) {
    String lowerTaskName = in_taskName.toLowerCase()
    if (lowerTaskName.contains('googleplay') == true) {
        return "googleplay"
    } else if (lowerTaskName.contains('amazon')){
        return "amazon"
    } else {
        System.err.println("Task '$taskName' does not specify a sku.")
        return "googleplay"
    }
}
/**
 * @author Ian Copland
 *
 * @param in_sku - The SKU name.
 *
 * @return Whether or not the given SKU is Google Play.
 */
def isGooglePlaySku(String in_sku) {
    String lowerSku = in_sku.toLowerCase()
    return (lowerSku.equals('googleplay') == true)
}
/**
 * @author Ian Copland
 *
 * @param in_sku - The SKU name.
 *
 * @return Whether or not the given SKU is Amazon.
 */
def isAmazonSku(String in_sku) {
    String lowerSku = in_sku.toLowerCase()
    return (lowerSku.equals('amazon') == true)
}

ext {
    /**
     * Adds task dependencies as required for building a ChilliSource library.
     *
     * @author Ian Copland
     *
     * @param in_tasks - The gradle tasks dependencies will be added to.
     * @param in_libName - The name of the library, without the lib prefix and .a suffix.
     */
    csInitLibrary = { in_tasks, in_libName ->
        in_tasks.all() { currentTask ->
            def taskName = currentTask.name.toLowerCase();

            //If it is the "clean" task also hook in the ChilliSource clean event.
            if (taskName == "clean") {
                def cleanTask = task("csCleanNdkStatic") << {
                    csNdkBuildCleanStatic(in_libName)
                }
                currentTask.dependsOn(cleanTask)

            //if it is a ndk compile task, replace it with the ChilliSource ndk compile task.
            } else if (taskName.startsWith("compile") == true && taskName.endsWith("ndk") == true) {

                currentTask.dependsOn.clear();
                currentTask.deleteAllActions();

                //exclude test tasks to avoid duplicate version of the created task.
                if (isTestFromTaskName(taskName) == false) {
                    def sku = getSkuFromTaskName(taskName)
                    def abi = getAbiFromTaskName(taskName)
                    def debug = isDebugFromTaskName(taskName)
                    def debugText = (debug == true) ? "debug" : "release"

                    def csCompileTask = task("csCompileNdk_${sku}_${abi}_${debugText}") << {
                        csNdkBuildStatic(sku, abi, debug, in_libName)
                    }

                    //This is a hack to ensure ChilliSource is only built for the ABI/SKU/config that the
                    //application is being built for. This has the draw-back that ChilliSource can no longer
                    //be built on its own, so hopefully a better solution presents itself in the future.
                    csCompileTask.onlyIf {
                        def taskGraph = gradle.taskGraph
                        for (def aTask : taskGraph.getAllTasks()) {
                            if (aTask.project.name.equals("app") && aTask.name.equals(csCompileTask.name) == true) {
                                return true
                            }
                        }
                        return false
                    }
                    currentTask.dependsOn(csCompileTask)
                }
            }
        }
    }
    /**
     * Adds task dependencies as requires for building a ChilliSource application.
     *
     * @author Ian Copland
     *
     * @param in_tasks - The gradle tasks dependencies will be added to.
     */
    csInitApplication = { in_tasks ->
        in_tasks.all() { currentTask ->
            def taskName = currentTask.name.toLowerCase();

            //if it is the 'clean' task also hook in the ChilliSource clean event.
            if (taskName == "clean") {
                def cleanTask = task("csCleanNdk") << {
                    csNdkBuildClean()
                }
                currentTask.dependsOn(cleanTask)

            //if it is a process manifest task, add the genate manifest task as a dependancy
            } else if (taskName.startsWith("check") == true && taskName.endsWith("manifest") == true) {
                task "csGenerateAndroidManifest" (overwrite: true) << {
                    csGenerateAndroidManifest("../../../..", "src/main")
                }
                currentTask.dependsOn("csGenerateAndroidManifest")

            //if it is a ndk compile task, replace it with the ChilliSource ndk compile task.
            } else if (taskName.startsWith("compile") == true && taskName.endsWith("ndk") == true) {

                currentTask.dependsOn.clear();
                currentTask.deleteAllActions();

                //exclude test tasks to avoid duplicate version of the created task.
                if (isTestFromTaskName(taskName) == false) {
                    def sku = getSkuFromTaskName(taskName)
                    def abi = getAbiFromTaskName(taskName)
                    def debug = isDebugFromTaskName(taskName)
                    def debugText = (debug == true) ? "debug" : "release"

                    task "csCompileNdk_${sku}_${abi}_${debugText}" (dependsOn: ":chillisource:csCompileNdk_${sku}_${abi}_${debugText}", overwrite: true) << {
                        csNdkBuild(sku, abi, debug)
                    }

                    task "csGenerateResources_${sku}_${abi}_${debugText}" (dependsOn: "csCompileNdk_${sku}_${abi}_${debugText}", overwrite: true) << {
                        if (isGooglePlaySku(sku) == true) {
                            csGenerateApkExpansion();
                        } else if (isAmazonSku(sku) == true) {
                            csGenerateApkAssets();
                        } else {
                            throw new GradleException("Unsupported SKU.");
                        }
                    }
                    currentTask.dependsOn("csGenerateResources_${sku}_${abi}_${debugText}")
                }
            }
        }
    }
    /**
     * Adds the default library dependencies required by ChilliSource.
     *
     * @author Ian Copland
     *
     * @param in_dependencies - The dependencies.
     */
    csAddDefaultDependencies = { in_dependencies ->
        in_dependencies.compile('com.android.support:appcompat-v7:21.0.3')
        in_dependencies.compile('com.google.android.gms:play-services:6.5.87')
        in_dependencies.compile('com.facebook.android:facebook-android-sdk:3.23.1')
        in_dependencies.googlePlayCompile(in_dependencies.project(path: ":playapkexpansion"))
        in_dependencies.googlePlayCompile(in_dependencies.project(path: ":playlicensing"))
    }
    /**
     * Android Gradle currently does not support dependencies for specific combinations of build
     * type and product flavour. This provides a means to handle it, by mapping application
     * build variant to a ChilliSource build variant.
     *
     * @author Ian Copland
     *
     * @param in_dependencies - The dependencies.
     * @param in_configurations - The configurations.
     * @param in_csDependencyMap - The map of application variants to ChilliSource variants.
     */
    csAddCSDependencies = { in_dependencies, in_configurations, in_csDependencyMap ->

        def depsMap = new HashMap<String, Dependency>()
        for (Map.Entry<String, String> entry : in_csDependencyMap) {
            depsMap.put("${entry.getKey()}Compile", in_dependencies.project(path: ":chillisource", configuration: entry.getValue()))
        }

        in_configurations.all() { config ->
            def dep = depsMap.get("${config.name}")
            if(dep != null) {
                config.dependencies.add(dep)
            }
        }
    }
    /**
     * External library projects are currently bugged such that they depend on all build variants
     * in a project. This causes a number of issues (and long build times!) for native projects,
     * so this can be used to solve the issue for the default dependencies.
     *
     * @author Ian Copland
     *
     * @param in_tasks - All tasks in the android project.
     */
    csFixDefaultLibraryDependancies = { in_tasks ->

        in_tasks.all() { task ->

            def androidSupportLibV7TaskName = "prepareComAndroidSupportAppcompatV72103Library"
            def androidSupportLibV4TaskName = "prepareComAndroidSupportSupportV42103Library"
            def googlePlayServicesLibTaskName = "prepareComGoogleAndroidGmsPlayServices6587Library"
            def facebookLibTaskName = "prepareComFacebookAndroidFacebookAndroidSdk3231Library"

            def taskName = task.name.toLowerCase()
            if (taskName.equals(androidSupportLibV7TaskName) == true || taskName.contains(androidSupportLibV4TaskName) == true ||
                taskName.equals(googlePlayServicesLibTaskName) == true || taskName.equals(facebookLibTaskName) == true) {
                task.dependsOn.clear()
            }
        }
    }
}